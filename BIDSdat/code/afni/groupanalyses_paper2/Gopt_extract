#!/bin/tcsh
#
#useage: Gout_extract

# The purpose of this script is to extract beta values for the cerebellum cluster with activation
# to Large-Small portions associated with quadratic portion size slopes 

# Written by Bari Fuchs Fall 2022
###################### set up initial variables  ###########################   
#don't log AFNI programs in ~/.afni.log
setenv AFNI_DONT_LOGFILE YES
 
#dont try version checks
setenv ANFI_VRSION_CHECK NO
 
##don't auto-compress output files
setenv AFNI_COMPRESSOR NONE

###################### setup and check directories  ###########################   
cd ../../../
set bidsdir = "$cwd"

#set lev1 dir
set lev1_dir =  $bidsdir/derivatives/analyses/foodcue-paper2/level1

#set level 2
set lev2_dir = $bidsdir/derivatives/analyses/foodcue-paper2/level2

#set map directory
set map_dir = $lev2_dir/feis_cerebellum/ped_fd-0.9_b20_3runs_noGSR_08-24-23

if ( ! -d $map_dir ) then
	echo "failed to find $map_dir"
	exit
endif

###################### Generate cluster mask  ###########################

#idat and ithr = 7 to select subbrick for quadratic slope covariate
3dClusterize 		\
	-nosum 			\
	-1Dformat 		\
	-inset $map_dir/quad_grams_1sampleT++_Large-Small_allED_GLT+tlrc.HEAD \
	-idat 7 -ithr 7 -NN 2 -clust_nvox 1300 -bisided -3.2905 3.2905 \
	-pref_map $map_dir/cerebellum_clust_mask-quad_grams001 \


###################### Extract from cluster mask  ###########################   

# delete beta text file 
if ( -f $map_dir/cerebellum_betas.txt ) then
	rm $map_dir/cerebellum_betas.txt
endif

# add headers to new file
echo sub ' \t ' large-small >> $map_dir/cerebellum_betas.txt

# loop through subjects
foreach i ( `cat $map_dir/index_quad_cerebellum.txt ` )
	set sub = $i
	set beta = `3dmaskave -mask $map_dir/cerebellum_clust_mask-quad_grams001+tlrc. $lev1_dir/sub-$sub/ped_fd-0.9_b20_noGSR/stats.sub-$sub+tlrc'[Large-Small_allED_GLT#0_Coef]'`
	echo $sub ' \t ' $beta >> $map_dir/cerebellum_betas.txt 
end
