#!/bin/tcsh
#
#useage: G2_feis   $1         $2       		$3						$4
#		     	template	execute?  	level 1 folder string 		mask
#
# The purpose of this script is to create and run correlation between amount contrast and feis variables within cerebellum mask
# $1: ped or MNI
# $2 run or no
# $3 e.g., fd-0.9_by-block-7_3blocks_noGSR, fd-0.9_b20_3runs_noGSR
# $4 appetitive or cerebellum

###################### set up initial variables  ###########################   
#don't log AFNI programs in ~/.afni.log
setenv AFNI_DONT_LOGFILE YES

#dont try version checks
setenv ANFI_VRSION_CHECK NO

##don't auto-compress output files
setenv AFNI_COMPRESSOR NONE

###################### setup and check directories  ###########################   
#go to and set BIDS main directory
cd ../../../
set bidsdir = "$cwd"

#set level1 dir
set lev1_dir =  $bidsdir/derivatives/analyses/foodcue-paper2/level1

# set mask_area
set mask_area = $4

#set test directory
set test_dir = $bidsdir/derivatives/analyses/foodcue-paper2/level2/cebq_${mask_area}

# set R dir
set R_dir = $bidsdir/derivatives/analyses/foodcue-paper2/R

# set R dir
set mask_dir = $bidsdir/derivatives/analyses/foodcue-paper2/masks

#set output name
set today = `date +%m-%d-%y`

# set level1 string
set lev1_str = $3

# set path to templates
set tpath = $bidsdir/derivatives/templates


##Get template specific results folder
if ( "$1" == "MNI" ) then
	##Get MNI template impelemented in 1_sswarp_strct
	set basedset = $tpath/tpl-MNI152NLin2009cAsym/tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz
	set basedset_name = "tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz"
	# set string
    	set temp = "MNI152NLin2009cAsym"
	set tempst = "MNI"
else
	##Get pediatric template used in fmriprep
	set basedset = $tpath/tpl-MNIPediatricAsym/cohort-3/tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz
	set basedset_name = "tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz"
	# set strings
	set temp = "MNIPediatricAsym_cohort-3"
	set tempst = "ped"
endif


# set lev1 results directory name
set lev1split = ($lev1_str:as/_/ /)
set lev1_results = ${tempst}_$lev1split[1]_$lev1split[2]_$lev1split[4]

# get censor str for index file
set censor_str = $lev1split[1]_$lev1split[2]_$lev1split[3]

#set map folder
set map_folder = $test_dir/${tempst}_${lev1_str}_${today}

#create map folder
if ( ! -d $map_folder ) then
    mkdir -p $map_folder
else
#	echo "$map_folder already exists. delete or rename to re-run"
#	exit
endif


# copy index list and covariate file into output folder for reference
cp $R_dir/index_fr_${mask_area}.txt $map_folder
cp $R_dir/index_sr_${mask_area}.txt $map_folder
cp $R_dir/fmri_covariates.csv $map_folder

# set infex files
set index_fr = $map_folder/index_fr_${mask_area}.txt
set index_sr = $map_folder/index_sr_${mask_area}.txt


###################### generate masks by index list  ########################

foreach analysis ("fr" "sr")

	# set index list
	if ( $analysis == 'fr' ) then
		set index = `cat $index_fr`
	endif

	if ( $analysis == 'sr' ) then
		set index = `cat $index_sr`
	endif

	# generate mask if it doesnt exist
	if ( ! -f $map_folder/${analysis}_${mask_area}_mask0.8+tlrc) then
		
		# make temporary directory with participant masks for subjects included in analyses
		mkdir $map_folder/temp_mask
		foreach sub ( $index )
			cp ${bidsdir}/derivatives/preprocessed/fmriprep/sub-${sub}/ses-1/func/foodcue_full_mask-${temp}*.nii $map_folder/temp_mask/mask_sub-${sub}.nii
		end

		# generate mask with 80% of participants overlapping
		3dmask_tool -input $map_folder/temp_mask/mask* \
						-prefix $map_folder/temp_mask/${analysis}_mask0.8+tlrc              \
						-frac 0.8

		# make intersection mask with 80% mask and functional area mask
      	3dmask_tool -input $map_folder/temp_mask/${analysis}_mask0.8+tlrc $mask_dir/${mask_area}_mask_resamp+tlrc 	\
					-prefix $map_folder/${analysis}_${mask_area}_mask0.8+tlrc \
                  	-frac 1.0

		# remove temporary directory
		rm -r $map_folder/temp_mask
	endif

end

###################### generate t-test scripts  ########################

# Note: all models include following covariates: sex, avg-motion, pre-mri fullness, pre-mri cams

# set index lists -- lists subjects to include in analyses
set index_fr_list = `cat $index_fr`
set index_sr_list = `cat $index_sr`

# set map name
set map = ( Large-Small_allED_GLT )

# add -Clustsim option to run 3dClustSim

# food responsiveness
gen_group_command.py -command 3dttest++                                             \
	-write_script $map_folder/fr_1sampleT++_${map}       \
	-prefix  fr_1sampleT_${map}           \
	-dsets ${lev1_dir}/*/${lev1_results}/stats.sub-???+tlrc.HEAD       \
	-dset_sid_list $index_fr_list 					\
	-set_labels $map                                                 \
	-subs_betas "${map}"'#'0_Coef                                    \
	-options                                                       \
		-mask $map_folder/fr_${mask_area}_mask0.8+tlrc.HEAD  -toz	\
		-covariates $map_folder/fmri_covariates.csv"'[0,13,15,16,19,20]'"

# satiety responsiveness
gen_group_command.py -command 3dttest++                                             \
	-write_script $map_folder/sr_1sampleT++_${map}      \
	-prefix  sr_1sampleT++_${map}        \
	-dsets ${lev1_dir}/*/${lev1_results}/stats.sub-???+tlrc.HEAD       \
	-dset_sid_list $index_sr_list 					\
	-set_labels $map                                                 \
	-subs_betas "${map}"'#'0_Coef                                    \
	-options                                                       \
		-mask $map_folder/sr_${mask_area}_mask0.8+tlrc.HEAD  -toz	\
		-covariates $map_folder/fmri_covariates.csv"'[0,14,15,16,19,20]'"

# set permissions of map folder
chmod 775 -R $map_folder

# Execute?
if ( "$2" == "run" ) then
	cd $map_folder
	tcsh fr_1sampleT++_${map}
	tcsh sr_1sampleT++_${map} 
endif



# Copy base template into results folder 

if ( ! -f $basedset ) then
    echo "***** Failed to find $basedset :("
    exit 1
else
    if ( ! -f $map_folder/$basedset_name ) then
        cp ${basedset} $map_folder
    endif
endif

