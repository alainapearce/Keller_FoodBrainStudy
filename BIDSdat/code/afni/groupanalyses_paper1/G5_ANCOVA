#!/bin/tcsh
#
#useage: G5_ANCOVA   $1               $2
#                    template    level 1 folder string
#         
# The purpose of this script is to compare portion size responses betweeh hED, lED and office blocks with 3dmvm
# $1: ped or MNI
# $2 e.g., fd-0.9_b20_3runs_noGSR

###################### set up initial variables  ###########################   
#don't log AFNI programs in ~/.afni.log
setenv AFNI_DONT_LOGFILE YES

#dont try version checks
setenv ANFI_VRSION_CHECK NO

##don't auto-compress output files
setenv AFNI_COMPRESSOR NONE

###################### setup and check directories  ###########################   
cd ../../../
set bidsdir = "$cwd"

#set level1 dir
set lev1_dir = $bidsdir/derivatives/analyses/foodcue-paper1/level1

# set level 2 dir
set lev2_dir = $bidsdir/derivatives/analyses/foodcue-paper1/level2

#set test directory
set test_dir = $lev2_dir/mvm

#set output name
set today = `date +%m-%d-%y`

# set level 1 str
set lev1_str = $2

# set path to templates
set tpath = $bidsdir/derivatives/templates/

#Get template specific variables
 if ( "$1" == "MNI" ) then
	##Get MNI template used in fmriprep
	set basedset = $tpath/tpl-MNI152NLin2009cAsym/tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz
	set basedset_name = "tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz"
	# set string
	set temp = "MNI152NLin2009cAsym"
	set tempst = "MNI"
else
	#Get pediatric template used in fmriprep
        set basedset = $tpath/tpl-MNIPediatricAsym/cohort-3/tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz
	set basedset_name = "tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz"
        # set strings
        set temp = "MNIPediatricAsym_cohort-3"
	set tempst = "ped"
endif

# split lev1 name
set lev1split = ($lev1_str:as/_/ /)

# get censor str for index file
set censor_str = $lev1split[1]_$lev1split[2]_$lev1split[3]

#set map folder
set map_folder = $test_dir/${tempst}_${lev1_str}_${today}

#create map folder
if ( ! -d $map_folder ) then
    mkdir -p $map_folder
else
	echo "$map_folder already exists. delete or rename to re-run"
	exit
endif

# copy index list and covariate file into output folder for reference
cp $lev2_dir/index_all_${censor_str}.txt $map_folder
cp $lev2_dir/mvm-table_${censor_str}.csv $map_folder


###################### generate mask for analyses  ########################
# set whole group index list -- lists subjects to include in mask
set WG_index = `cat $lev2_dir/index_all_${censor_str}.txt`

# make temporary directory with participant masks for subjects included in analyses
mkdir $map_folder/temp_mask
foreach sub ( $WG_index )
	cp ${bidsdir}/derivatives/preprocessed/fmriprep/sub-${sub}/ses-1/func/foodcue_full_mask-${temp}*.nii $map_folder/temp_mask/mask_sub-${sub}.nii
end

# generate mask with 80% of participants overlapping
3dmask_tool -input $map_folder/temp_mask/mask* \
	-prefix $map_folder/WG_mask0.8+tlrc              \
        -frac 0.8

# remove temporary directory
rm -r $map_folder/temp_mask

###################### generate scripts  ########################

3dMVM -prefix "ancova_ps" -jobs 24        \
          -mask $map_folder/WG_mask0.8+tlrc               \
          -bsVars  "sex+ff_premri+fd_avg"  \
          -wsVars "cuetype"                \
          -qVars  "ff_premri,fd_avg"               \
          -dataTable @$map_folder/mvm-table_${censor_str}.csv

# Copy base template into results folder 
if ( ! -f $basedset ) then
    echo "***** Failed to find $basedset :("
    exit 1
else
    if ( ! -f $map_folder/$basedset_name ) then
        cp ${basedset} $map_folder
    endif
endif



