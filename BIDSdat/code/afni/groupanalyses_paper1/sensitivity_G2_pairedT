#!/bin/tcsh
#
#useage: sensitivity_G2_pairedT   $1            $2        $3
#		     				template	  execute?  level 1 folder string
#
# The purpose of this script is to create and run 2 sample paired t-tests on the Portion Size response - including snack intake as covariate
# $1: ped or MNI
# $2 run or no
# $3 e.g., fd-0.9_by-block-7_3blocks_noGSR, ped_fd-0.9_b20_noGSR

###################### set up initial variables  ###########################   
#don't log AFNI programs in ~/.afni.log
setenv AFNI_DONT_LOGFILE YES

#dont try version checks
setenv ANFI_VRSION_CHECK NO

##don't auto-compress output files
setenv AFNI_COMPRESSOR NONE

###################### setup and check directories  ###########################   
#go to and set BIDS main directory
cd ../../../
set bidsdir = "$cwd"

#set level1 dir
set lev1_dir = $bidsdir/derivatives/analyses/foodcue-paper1/level1

#set test directory
set test_dir = $bidsdir/derivatives/analyses/foodcue-paper1/level2/2sample-pairedT_sensitivity

# set level 2 dir
set lev2_dir = $bidsdir/derivatives/analyses/foodcue-paper1/level2

#set output name
set today = `date +%m-%d-%y`

# set level1 string
set lev1_str = $3

# set path to templates
set tpath = $bidsdir/derivatives/templates/

##Get template specific results folder
if ( "$1" == "MNI" ) then
	##Get MNI template impelemented in 1_sswarp_strct
	set basedset = $tpath/tpl-MNI152NLin2009cAsym/tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz
	set basedset_name = "tpl-MNI152NLin2009cAsym_res-01_T1w.nii.gz"
	# set string
    	set temp = "MNI152NLin2009cAsym"
	set tempst = "MNI"
else
	##Get pediatric template used in fmriprep
	set basedset = $tpath/tpl-MNIPediatricAsym/cohort-3/tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz
	set basedset_name = "tpl-MNIPediatricAsym_cohort-3_res-1_T1w.nii.gz"
	# set strings
	set temp = "MNIPediatricAsym_cohort-3"
	set tempst = "ped"
endif


# set lev1 results directory name
set lev1split = ($lev1_str:as/_/ /)
set lev1_results = ${tempst}_$lev1split[1]_$lev1split[2]_$lev1split[4]

# get censor str for index file
set censor_str = $lev1split[1]_$lev1split[2]_$lev1split[3]

#set map folder
set map_folder = $test_dir/${tempst}_${lev1_str}_${today}

#create map folder
if ( ! -d $map_folder ) then
    mkdir -p $map_folder
else
	echo "$map_folder already exists. delete or rename to re-run"
	exit
endif

# set index list -- lists subjects to include in analyses
set index = `cat $lev2_dir/index_sensitivity_all_${censor_str}.txt`
	
# copy index list and covariate file into output folder for reference
cp $lev2_dir/index_sensitivity_all_${censor_str}.txt $map_folder
cp $lev2_dir/ttest-covariates.txt $map_folder

###################### generate mask for whole group (WG) ########################

# make temporary directory with participant masks for subjects included in analyses
mkdir $map_folder/temp_mask
   
foreach sub ( $index )
	cp ${bidsdir}/derivatives/preprocessed/fmriprep/sub-${sub}/ses-1/func/foodcue_full_mask-${temp}*.nii $map_folder/temp_mask/mask_sub-${sub}.nii
end

# generate mask with 80% of participants overlapping
3dmask_tool -input $map_folder/temp_mask/mask* \
        -prefix $map_folder/WG_mask0.8+tlrc              \
        -frac 0.8

    # remove temporary directory
rm -r $map_folder/temp_mask

###################### generate t-test scripts  ########################
# gen_group_command.py settings:
	# add -Clustsim option to run 3dClustSim

# set ouput names
set output_name_HvL = "High_v_Low_PScon"
set output_name_FvO = "Food_v_Office_PScon"

# generate ttest script
gen_group_command.py -command 3dttest++                                            \
	    -write_script $map_folder/WG_2sampleT++_${output_name_HvL}           \
	    -prefix  WG_2sampleT++_${output_name_HvL}                         \
	    -dsets $lev1_dir/*/$lev1_results/stats.sub-???+tlrc.HEAD               \
	    -dset_sid_list $index						   \
	    -set_labels HighLarge-Small LowLarge-Small                             \
	    -subs_betas HighLarge-Small_GLT'#'0_Coef LowLarge-Small_GLT'#'0_Coef   \
	    -options	-Clustsim								   \
			-paired -mask $map_folder/WG_mask0.8+tlrc -covariates $lev2_dir/ttest-covariates.txt"'[0..3,6,9]'"

gen_group_command.py -command 3dttest++                                            \
	    -write_script $map_folder/WG_2sampleT++_${output_name_FvO}          \
	    -prefix  WG_2sampleT++_${output_name_FvO}                          \
	    -dsets $lev1_dir/*/$lev1_results/stats.sub-???+tlrc.HEAD               \
	    -dset_sid_list $index						   \
	    -set_labels Large-Small_allED OfficeLarge-Small                             \
	    -subs_betas Large-Small_allED_GLT'#'0_Coef OfficeLarge-Small_GLT'#'0_Coef   \
	    -options	-Clustsim							   \
			-paired -mask $map_folder/WG_mask0.8+tlrc -covariates $lev2_dir/ttest-covariates.txt"'[0..3,6,9]'"


# set permissions
chmod 775 $map_folder/*

# execute script?
if ( "$2" == "run" ) then
	cd $map_folder
	tcsh WG_2sampleT++_${output_name_HvL}
	tcsh WG_2sampleT++_${output_name_FvO}

endif

# Copy base template into results folder 

if ( ! -f $basedset ) then
    echo "***** Failed to find $basedset :("
    exit 1
else
    if ( ! -f $map_folder/$basedset_name ) then
        cp ${basedset} $map_folder
    endif
endif

